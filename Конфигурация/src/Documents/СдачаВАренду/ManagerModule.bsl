
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура заполняет табличный документ для печати.
//
// Параметры:
//	ТабДок - ТабличныйДокумент - табличный документ для заполнения и печати.
//	Ссылка - Произвольный - содержит ссылку на объект, для которого вызвана команда печати.
Процедура СчетНаОплату(ТабДок, Ссылка) Экспорт

	Макет = Документы.СдачаВАренду.ПолучитьМакет("СчетНаОплату");
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ СдачаВАренду.Номер,
		|	СдачаВАренду.Дата,
		|	СдачаВАренду.Клиент,
		|	СдачаВАренду.Договор,
		|	СдачаВАренду.Ответственный,
		|	СдачаВАренду.Квартира,
		|	СдачаВАренду.ЦенаЗаСутки,
		|	СдачаВАренду.Сумма,
		|	СдачаВАренду.ДатаНачала,
		|	СдачаВАренду.ДатаОкончания
		|ИЗ
		|	Документ.СдачаВАренду КАК СдачаВАренду
		|ГДЕ
		|	СдачаВАренду.Ссылка В (&Ссылка)";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Подвал = Макет.ПолучитьОбласть("Подвал");
	
	ТабДок.Очистить();

	ВставлятьРазделительСтраниц = Ложь;
	Пока Выборка.Следующий() Цикл
		Если ВставлятьРазделительСтраниц Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПараметрыПредмета="рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2";
		СуммаДокументаПрописью = ЧислоПрописью(Выборка.Сумма,, ПараметрыПредмета);

		ПараметрыЗаполнения = Новый Структура;
		ПараметрыЗаполнения.Вставить("Дата", Формат(Выборка.Дата, "ДЛФ=DD;"));
		ПараметрыЗаполнения.Вставить("Номер", УдалитьЛидирующиеНули(Выборка.Номер));
		ПараметрыЗаполнения.Вставить("СуммаПрописью", СуммаДокументаПрописью);
		
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыЗаполнения);
		
		ТабДок.Вывести(ОбластьЗаголовок);
		
		Шапка.Параметры.Заполнить(Выборка);
		ТабДок.Вывести(Шапка, Выборка.Уровень());	
		
		Подвал.Параметры.Заполнить(Выборка);
		Подвал.Параметры.Заполнить(ПараметрыЗаполнения);
		ТабДок.Вывести(Подвал);	
		
		ВставлятьРазделительСтраниц = Истина;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УдалитьЛидирующиеНули(Номер)
	
	Результат = Номер;
	
	Пока СтрНачинаетсяС(Результат, "0") Цикл
		Результат = Сред(Результат, 2);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#КонецЕсли